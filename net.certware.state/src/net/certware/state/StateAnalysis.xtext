grammar net.certware.state.StateAnalysis with org.eclipse.xtext.common.Terminals

generate stateAnalysis "http://www.certware.net/state/StateAnalysis"

System:
	'System'
	name=ID
	description=STRING
	'subsystems' '{' subsystems+=Subsystem* '}'
	;
	
Subsystem:
	'Subsystem' 
	name=ID 
	description=STRING
	'sets' '{' deploymentSets += DeploymentSet* '}'
;
	
DeploymentSet:
	'DeploymentSet'
	name=ID
	description=STRING
	'deployments' '{' deployments += Deployment* '}'
;

QosPolicy:
	'QosPolicy'
	name=ID
	description=STRING
	'policy' policy=STRING
;

DataProduct:
	'DataProduct'
	name=ID
	description=STRING
	'content' content=STRING
;

DataCollection:
	'DataCollection'
	name=ID
	description=STRING
	'policies' '{' policies += QosPolicy* '}'
	'products' '{' products += DataProduct* '}'
;

Deployment:
	'Deployment'
	name=ID
	description=STRING
	'components' '{' components += Component* '}'
	'catalogs ' '{' catalogs += DataCollection* '}'
;
	
Component:
	'Component'
	name=ID
	description=STRING
	'variables' '{' stateVariables += StateVariable+ '}'
	'controllers' '{' controllers += Controller+ '}'
	'estimators' '{' estimators += Estimator+ '}'
	'adapters' '{' adapters += HardwareAdapter+ '}'
;
	
StateRepresentationType:
	'TIMELINE' | 'DERIVED' | 'GRAPH'
;

StateVariable:
	'StateVariable'
	name=ID
	description=STRING
	'type' representationType=StateRepresentationType
	'in' '{' inStateConstraints += StateConstraint* '}'
	'out' '{' outStateConstraints += StateConstraint* '}'
	'updates' '{' stateUpdates += StateUpdate* '}'
	'influenced' '{' influencedBy += StateVariable* '}'
;

Controller:
	'Controller'
	name=ID
	description=STRING
	'delegates' '{' delegates += [Controller]* '}'
	'constraints' '{' stateConstraint += StateConstraint+ '}'
	'commands' '{' hardwareCommand += HardwareCommand+ '}'
;

Estimator:
	'Estimator'
	name=ID
	description=STRING
	'distills' '{' distilledMeasurements += [Estimator]* '}'
	'constraints' '{' stateConstraints += StateConstraint* '}'
	'updates' '{' stateUpdates += StateUpdate+ '}'
	'measurement' '{' hardwareMeasurement += HardwareMeasurement+ '}'
;

StateConstraint:
	'Constraint'
	name=ID
	description=STRING
;

StateUpdate:
	'StateUpdate'
	name=ID
	description=STRING
;

HardwareCommand:
	'HardwareCommand'
	name=ID
	description=STRING
;

HardwareMeasurement:
	'HardwareMeasurement'
	name=ID
	description=STRING
;

Device:
	'Device'
	name=ID
	description=STRING
	'deviceCommands' '{' deviceCommands += DeviceCommand* '}'
	'deviceMeasurements' '{' deviceMeasurements += DeviceMeasurement* '}'	
;

DeviceCommand:
	'DeviceCommand'
	name=ID
	description=STRING
;

DeviceMeasurement:
	'DeviceMeasurement'
	name=ID
	description=STRING
;

HardwareAdapter:
	'HardwareAdapter'
	name=ID
	description=STRING
	'hardwareCommands' '{' hardwareCommands += HardwareCommand* '}'
	'hardwareMeasurements' '{' hardwareMeasurements += HardwareMeasurement* '}'
	'deviceCommands' '{' deviceCommands += DeviceCommand* '}'
	'deviceMeasurements' '{' deviceMeasurements += DeviceMeasurement* '}'
;